plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'io.freefair.lombok' version '8.6'
    id 'nu.studer.jooq' version '9.0'
    id 'org.liquibase.gradle' version '2.2.1'
}

java {
    sourceCompatibility = targetCompatibility = '17'
}

ext {
    grpcVersion = '1.62.2'
    protocVersion = '3.25.3'
    javaxAnnotationVersion = '1.3.2'
    logbackVersion = '1.5.3'
    mapstructVersion = '1.5.5.Final'
    liquibaseVersion = '4.27.0'
    picocliVersion = '4.7.5'
    jooqVersion = '3.19.7'
    postgresqlVersion = '42.7.3'
    hikariVersion = '5.1.0'
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    implementation "com.zaxxer:HikariCP:$hikariVersion"
    implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"

    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    jooqGenerator "org.postgresql:postgresql:$postgresqlVersion"

    liquibaseRuntime "org.liquibase:liquibase-core:$liquibaseVersion"
    liquibaseRuntime "info.picocli:picocli:$picocliVersion"
    liquibaseRuntime "org.postgresql:postgresql:$postgresqlVersion"

    runtimeOnly "org.postgresql:postgresql:$postgresqlVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

liquibase {
    activities {
        createTables {
            changelogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
            driver 'org.postgresql.Driver'
            url project.ext.url
            username project.ext.username
            password project.ext.password
        }
    }
}

jooq {
    version = "$jooqVersion"
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = project.ext.url
                    user = project.ext.username
                    password = project.ext.password
                }
                generator {
                    database {
                        inputSchema = 'public'
                        excludes = 'databasechangelog|databasechangeloglock'
                    }
                    generate {
                        pojosEqualsAndHashCode = true
                        relations = true
                        deprecated = false
                        records = true
                        pojos = true
                        immutablePojos = false
                        fluentSetters = true
                    }
                    target {
                        packageName = 'com.grigoryev.flowoffate'
                        directory = 'build/generated/sources/jooq/main'
                    }
                }
            }
        }
    }
}

generateJooq.dependsOn(update)

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += [
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ]
}
