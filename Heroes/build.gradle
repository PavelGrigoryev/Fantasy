plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
    id 'io.freefair.lombok' version '8.6'
}

java {
    sourceCompatibility = targetCompatibility = '17'
}

ext {
    grpcVersion = '1.62.2'
    protocVersion = '3.25.3'
    javaxAnnotationVersion = '1.3.2'
    logbackVersion = '1.5.3'
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"

    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
